dnl Process this file with autoconf to create configure.

AC_INIT(config.h.in)

dnl ################################################################
dnl # Version information
dnl ################################################################
TVB_MAJOR_VERSION=0
TVB_MINOR_VERSION=1
TVB_MICRO_VERSION=3

TVB_VERSION=$TVB_MAJOR_VERSION.$TVB_MINOR_VERSION.$TVB_MICRO_VERSION
AC_DEFINE_UNQUOTED(TVB_MAJOR_VERSION, $TVB_MAJOR_VERSION,
	[tvbuggy major version.])
AC_DEFINE_UNQUOTED(TVB_MINOR_VERSION, $TVB_MINOR_VERSION,
	[tvbuggy minor version.])
AC_DEFINE_UNQUOTED(TVB_MICRO_VERSION, $TVB_MICRO_VERSION,
	[tvbuggy micro version.])
AC_DEFINE_UNQUOTED(TVB_VERSION, $TVB_VERSION,
	[tvbuggy version.])

dnl ################################################################
dnl # Initialize automake
dnl ################################################################
VERSION=$TVB_VERSION
PACKAGE=tvbuggy

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

dnl ################################################################
dnl # Specify a header configuration file
dnl ################################################################
AM_CONFIG_HEADER(config.h)

dnl ################################################################
dnl # Check for some standard stuff.
dnl ################################################################
AC_PROG_CC
AC_PROG_CXX
AC_ARG_PROGRAM
AC_PROG_CPP
AC_PROG_MAKE_SET
AC_PROG_LN_S
AC_PROG_INSTALL

AC_C_CONST
AC_TYPE_MODE_T
AC_TYPE_SIZE_T
AC_EXEEXT
AC_HEADER_STDC

dnl ################################################################
dnl # Checks for GNUpdate
dnl ################################################################
AM_WITH_DMALLOC
AM_WITH_LEAKBUG

dnl ################################################################
dnl # Check for libxml
dnl ################################################################
AM_PATH_XML2(,
[
    AC_DEFINE(HAVE_LIBXML2, 1, [Define if you have libxml 2.x])
    XML_PC_DEP=libxml-2.0
],
[
    AM_PATH_XML(,
    [
        AC_DEFINE(HAVE_LIBXML1, 1, [Define if you have libxml 1.x])
        XML_PC_DEP=libxml
    ],
    [
        AC_MSG_ERROR([libxml is required! Please download from http://www.xmlsoft.org])
    ])
])

AC_SUBST(XML_PC_DEP)


dnl # Use wall if we have GCC
if test "x$GCC" = "xyes"; then
    CFLAGS="$CFLAGS -Wall"
fi

dnl ################################################################
dnl # Output the Makefiles
dnl ################################################################
AC_OUTPUT([
tvbuggy.spec
Makefile
src/Makefile
])

echo
echo "Now type make to compile."
echo "Then su to root and type: make install"
echo

