#
# rcpod build script for SCons (http://www.scons.org)
#


########### Directories and source files


libSrc = [
    'device.c',
    'command.c',
    'i2c.c',
    ]
examples = [
    'blinkytest',
    'hexdump',
    'adc_cat',
    'reset',
    ]
libs = ['usb']
libHeader = 'include/rcpod.h'
prefix = '/usr'


########### Build targets


# Debug symbols
env = Environment(CCFLAGS='-g -Werror')

# Add our local include directory to the C preprocessor path
env['CPPPATH'] = ['include']


# Build shared and static libraries
# FIXME: The shared lib prefix nonsense here is to prevent a
#        "Two different sets of overrides were specified for the same target"
#        when building the python extension.
#
libSrcPaths = ["src/%s" % file for file in libSrc]
sharedLib = env.SharedLibrary('librcpod', libSrcPaths, LIBS=libs, SHLIBPREFIX='')
staticLib= env.StaticLibrary('rcpod', libSrcPaths)


# Build the example programs, using the static library
# (it can be a pain to use shared libs without installing them...)
for example in examples:
    env.Program(example, ['examples/%s.c' % example, staticLib], LIBS=libs)


# librcpod installation
libDir = "%s/lib" % prefix
includeDir = "%s/include" % prefix
installPaths = [libDir, includeDir]
env.Install(dir=libDir, source=sharedLib)
env.Install(dir=includeDir, source=libHeader)


# Optional targets, only enabled if we have swig available
if 'swig' in env['TOOLS']:
    print " - Found SWIG, building Python package"

    # There doesn't seem to be any good way to do this yet...
    # Guess at the path to Python.h by sticking the current python
    # version onto /usr/include, then add this to our CPPPATH so
    # our swig-generated module can find Python.h
    import sys, os
    pyHeaderPath = "/usr/include/python%d.%d" % sys.version_info[:2]
    pyInstallPath = "/usr/lib/python%d.%d/site-packages" % sys.version_info[:2]
    if os.path.isfile(os.path.join(pyHeaderPath, "Python.h")):
        # Yay, we do have a valid path to Python.h
        env['CPPPATH'].append(pyHeaderPath)

        # Give swig access to our local include directory, and tell it
        # to generate output for Python
        env['SWIGFLAGS'] = '-Iinclude -python'

        # Compile the python module with librcpod linked statically,
        # out of convenience. This way librcpod doesn't have to be installed
        # for the python module to be usable. We want to override the
        # shared library prefix, since it isn't needed or wanted for building
        # dynamically loadable python extensions.
        pylibrcpod = env.SharedLibrary('pyrcpod/_librcpod',
                                       ['pyrcpod/librcpod.i'] + libSrcPaths,
                                       LIBS=libs, SHLIBPREFIX='')

	# If we also have a valid installation path, use it
        if os.path.isdir(pyInstallPath):
            modPath = os.path.join(pyInstallPath, "pyrcpod")
            installPaths.append(modPath)
            env.Install(dir=modPath, source=pylibrcpod)
            env.Install(dir=modPath, source='pyrcpod/__init__.py')
            env.Install(dir=modPath, source='pyrcpod/librcpod.py')
            env.Install(dir=modPath, source='pyrcpod/device.py')
	    env.Install(dir=modPath, source='pyrcpod/board.py')
            i2cModPath = os.path.join(modPath, "i2c")
            installPaths.append(i2cModPath)
            env.Install(dir=i2cModPath, source='pyrcpod/i2c/__init__.py')
            env.Install(dir=i2cModPath, source='pyrcpod/i2c/generic.py')
            env.Install(dir=i2cModPath, source='pyrcpod/i2c/maxim.py')
            env.Install(dir=i2cModPath, source='pyrcpod/i2c/microchip.py')

    else:
        print " - Can't guess the path to Python.h, not building the Python package"
else:
    print " - SWIG not found, not building the Python package"


env.Alias('install', installPaths)

### The End ###
