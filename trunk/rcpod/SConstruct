#
# rcpod build script for SCons (http://www.scons.org)
#


########### Directories and source files

libSrc = [
    'device.c',
    'command.c',
    ]
testProgs = [
    'blinkytest',
    'hexdump',
    'adc_cat',
    ]
libs = ['usb']
libHeader = 'include/rcpod.h'
prefix = '/usr'


########### Build targets

# Debug symbols
env = Environment(CCFLAGS='-g -Werror')

# Add our local include directory to the C preprocessor path
env['CPPPATH'] = 'include'

# Build shared and static libraries
libSrcPaths = ["src/%s" % file for file in libSrc]
sharedLib = env.SharedLibrary('rcpod', libSrcPaths, LIBS=libs)
staticLib= env.StaticLibrary('rcpod', libSrcPaths)

# Build the test programs, using the static library
# (it can be a pain to use shared libs without installing them...)
for testName in testProgs:
    env.Program(testName, ['test/%s.c' % testName, staticLib], LIBS=libs)

# Installing librcpod
libDir = "%s/lib" % prefix
includeDir = "%s/include" % prefix
env.Alias('install', [libDir, includeDir])
env.Install(dir=libDir, source=sharedLib)
env.Install(dir=includeDir, source=libHeader)

### The End ###
