<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  <xsl:output method="html"/>

  <xsl:template match="kbpage">
    <html>
      <head>
        <title><xsl:value-of select="@title"/></title>
        <style>
          body {
            font-family: Helvetica;
            background-image: url(glass.png);
            background-attachment: fixed;
            background-repeat: no-repeat;
            background-position: bottom right;
          }
          
          .section-link {
            color: #dddddd;
            text-decoration: none;
            float: right;
          }

          .section-link:hover {
            text-decoration: underline;
          }

          .section-header {
            font-size: larger;
            font-weight: heavy;
            border: 1px gray dotted;
            border-width: 0px 0px 1px 0px;
            min-width: 90%;
            margin:5px;
            color: gray;
          }

          .title {
            font-size: xx-large;
            align: middle;
            background: lightgrey;
            border-bottom: 1px black solid;
            padding-bottom: 4px;
          }

          .type:before {
            content: "$  ";
          }
          
          .type {
            background: black;
            font-size: +1;
            color: white;
            margin: 4px;
            margin-left: 2em;
            padding: 4px;
            border: 1px dotted lightgrey;
          }

          .backlink {
            font-size: small;
            padding-right: 2em;
            color: grey;
          }
        </style>
      </head>

      <body style="margin: 0px">
        <div class="title">
          <table borderwidth="0">
            <tr>
              <td width="100%"><a href="index.xml"><img align="middle" src="wine.png" border="0"/></a> <xsl:value-of select="@title"/></td>
              <xsl:if test="@backlink">
                <td nowrap="true" class="backlink"><a href="{@backlink}.xml">&lt;&lt; back to <xsl:value-of select="@backlink"/></a></td>
              </xsl:if>
            </tr>
          </table>
        </div>

        <div style="padding: 10px">
          <xsl:apply-templates/>
        </div>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="section">
    <a name="{@title}"/>
    <div class="section-header"><a class="section-link" href="#{@title}">#</a> <xsl:value-of select="@title"/></div>
    <pre style="font-size: large">
      <xsl:apply-templates/>
    </pre>
  </xsl:template>

  <xsl:template match="l">
    <a href="{node()}.xml"><xsl:value-of select="node()"/></a>
  </xsl:template>

  <xsl:template match="type">
    <div class="type"> <xsl:apply-templates/> </div>
  </xsl:template>

  <xsl:template match="email">
    <div style="color: blue; display:inline; unicode-bidi:bidi-override; direction: rtl;"><xsl:value-of select="node()"/></div>
  </xsl:template>
  
  <xsl:template match="@*|node()">
    <xsl:copy>
      <xsl:apply-templates select="@*|node()"/>
    </xsl:copy>
  </xsl:template>

</xsl:stylesheet>
